// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    And(a=instruction[15],b=instruction[3],out=writeM);
    Not(in=instruction[5],out=nins5);
    Nand(a=instruction[15],b=nins5,out=ins5);
    And(a=instruction[15],b=instruction[4],out=ins4);
    And(a=instruction[15],b=instruction[12],out=AorM);
    Mux16(a=instruction,b=ALUout,sel=instruction[15],out=inA);
    ARegister(in=inA,load=ins5,out=A,out[0..14]=addressM);
    DRegister(in=ALUout,load=ins4,out=x);
    Mux16(a=A,b=inM,sel=AorM,out=y);
    ALU(x=x,y=y,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],out=outM,out=ALUout,ng=ng,zr=zr);
    Or(a=ng,b=zr,out=nps);
    Not(in=nps,out=ps);
    And(a=instruction[15],b=instruction[0],out=ins0);
    And(a=instruction[15],b=instruction[1],out=ins1);
    And(a=instruction[15],b=instruction[2],out=ins2);
    And(a=ins0,b=ps,out=jump0);
    And(a=ins1,b=zr,out=jump1);
    And(a=ins2,b=ng,out=jump2);
    Or(a=jump0,b=jump1,out=jump01);
    Or(a=jump01,b=jump2,out=jump);
    PC(in=A,reset=reset,load=jump,inc=true,out[0..14]=pc);
}